plugins {
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id 'maven-publish'
	id 'com.gradleup.shadow' version '9.0.0+'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	mavenCentral()
}

loom {
	mods {
		"blocketing" {
			sourceSet sourceSets.main
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Discord JDA
	shadow(implementation("net.dv8tion:JDA:5.5.1"))

	// Server Metrics
	shadow(implementation("com.github.oshi:oshi-core:6.6.5"))
	shadow(implementation("net.java.dev.jna:jna:5.15.0"))
	shadow(implementation("net.java.dev.jna:jna-platform:5.15.0"))
}

tasks.shadowJar {
	configurations = [project.configurations.shadow]
	archiveClassifier.set("dev")
	relocate 'net.dv8tion.jda', 'com.blocketing.shadow.jda'

	relocate 'oshi',               'com.blocketing.shadow.oshi'
	relocate 'com.sun.jna',        'com.blocketing.shadow.jna'
	relocate 'org.slf4j',          'com.blocketing.shadow.slf4j'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

tasks.remapJar {
	inputFile.set(tasks.shadowJar.archiveFile)
	dependsOn(tasks.shadowJar)
	archiveClassifier.set("")
}